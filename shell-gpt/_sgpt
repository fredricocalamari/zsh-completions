#compdef sgpt

_sgpt_models() {
  local -a models=(
    'gpt-4-turbo-preview'
    'gpt-4-0125-preview'
    'gpt-4-1106-preview'
    'gpt-4-vision-preview'
    'gpt-4-1106-vision-preview'
    'gpt-4'
    'gpt-4-0613'
    'gpt-4-32k'
    'gpt-4-32k-0613'
    'gpt-3.5-turbo'
  )
  _wanted models expl 'available models' compadd -a models
}

_sgpt_chats() {
  local -a chats=("${(@f)$(sgpt --list-chats 2>/dev/null)}")
  chats=("${(@)chats##*/}")
  _wanted chats expl 'available chats' compadd -a chats
}

_sgpt_roles() {
  local -a roles=("${(@f)$(sgpt --list-roles 2>/dev/null)}")
  roles=("${(@)roles##*/}")
  roles=("${(@)roles%.json}")
  _wanted roles expl 'available roles' compadd -a roles
}

_sgpt() {
  local -a options=(
    '--model[Large language model to use.]:model:_sgpt_models'
    '--temperature[Randomness of generated output.]:temperature:'
    '--top-p[Limits highest probable tokens (words).]:top-p:'
    '--no-md[Disable prettify markdown output.]'
    '--editor[Open $EDITOR to provide a prompt.]'
    '--no-cache[Disable caching of completion results.]'
    '--version[Show version.]'
    '--help[Show this message and exit.]'
    '--shell[Generate and execute shell commands.]:shell:'
    '--no-interaction[Disable interactive mode for --shell option.]'
    '--describe-shell[Describe a shell command.]:describe-shell:'
    '--code[Generate only code.]:code'
    '--no-functions[Disallow function calls.]'
    '--chat[Follow conversation with id, use "temp" for quick session.]:chat:_sgpt_chats'
    '--repl[Start a REPL (Read–eval–print loop) session.]:repl:_sgpt_chats'
    '--show-chat[Show all messages from provided chat id.]:show-chat:_sgpt_chats'
    '--list-chats[List all existing chat ids.]'
    '--role[System role for GPT model.]:role:_sgpt_roles'
    '--create-role[Create role.]:create-role:'
    '--show-role[Show role.]:show-role:'
    '--list-roles[List roles.]'
  )

  _message -r 'ShellGPT: Specify options'
  _arguments -C \
      '1:prompt:->prompt' \
      '*:options:->options' \
      $options &&
      return 0
      # Prompt is matched first because it is optional in 1st field
      # additional options wont match unless
}
